#### 1.and与or指令
**1.and指令：逻辑与指令，按位进行运算**
```
mov al,01100011B
and  al,00111011B
执行后： al = 00100011B
相同的位置不变，其余为0   操作对象相应位设置为0其余不变
```
**2.or指令：逻辑或指令，按位进行或运算**
```
mov al,0100011B
or    al,00111011B
执行后：al=01111011B  
为1的地方则是1，都为0时候才是0  操作对象的相应位设为1，其他不变
```
#### 2.以字符形式给出数据
汇编中可以使用' ... '的方式以字符形式给出数据
编译器将他们转换为相对应的<font color=yellow>ASCII码，以16进制进制显示存放再内存中,转换为10进制即可对照ASCII码表找到</font>

```
db 'unIX'
db 'foRK'
```
#### 3.大小写转换问题
大写字母+20就是小写字母
小写字母-20就是大写字母
新的规律：
>ASCII二进制来看，除了第5位（从零开始计算），大写字母和小写字母其他各位都一样。大写字母ASCII码的第5位是0，小写字母的第5位是1。
```
assume cs:code,ds:datasg

code segment
	
datasg segment
	db 'BaSiC'
	db 'iNfOrMaTiOn'
datasg ends

code segment
	
     start:
     	
	mov ax,datasg
	mov ds,ax	;设置ds指向datasg段
	mov bx,0	;设置bx=0，ds:bx指向'BaSiC'的第一个字母
	mov cx,5	;设置循环次数5，因为'BaSiC'有五个字母
    
     s: mov al,[bx]     ;将ASCII码从ds:bx所指向的单元中取出
     	and al,11011111B;将al中的ASCII码的第5个位置为0，变为大写字母
	mov [bx],al	;将转变后的ASCII码写回源单元
	inc bx		;bx+1 ds:bx指向下一个字母
	loop s

	mov bx,5	;设置(bx)=5,ds:bx指向'iNfOrMaTiOn'的第一个字母
	mov cx,11	;设置循环次数11，因为'iNfOrMaTiOn'有11个字母
    s0: mov al,[bx]	
    	or al,00100000B	;将al中的ASCII码的第5个位置为1，变为小写字母
	mov [bx],al
	inc bx
	loop s0

	mov ax,4c00h
	int 21h


code ends

end start

```
#### 4.\[bx+idata]
前面使用\[bx]的方式来指明一个内存单元，还可以用一种更为灵活的方式来指明内存单元：
>\[bx+idata]表示一个内存单元，他的偏移地址为(bx) + idata(bx中的数值加上idata)

指令 mov ax,\[bx+200]
>将一个内存单元的内容送入ax，这个内存单元的长度为2个字节（字单元），存放一个字，偏移地址为bx中的数值加上200，段地址在ds中
>数学化描述为：==(ax)=((ds)\*16+(bx)+200)==

该指令也可以写成如下格式(常用)：
```
mov ax,[200+bx]
mov ax,200[bx]
mov ax,[bx].200
```
#### 5.用\[bx+idata]的方式进行数组的处理
用\[bx+idata]方式进行大小写转换
```

assume cs:code,ds:datasg

datasg segment
	db 'BaSiC'
	db 'MinIX'
datasg ends

code segment
start:
     	
	mov ax,datasg
	mov ds,ax	
	mov bx,0	
	mov cx,5	
    
     s: mov al,[bx]        ;定位第一个字符串中的字符
     	and al,11011111B
	mov [bx],al	

	mov al,[bx+5]	   ;定位第二个字符串的字符
    	or al,00100000B	
	mov [bx+5],al

	inc bx
	loop s

	mov ax,4c00h
	int 21h
code ends
end start
```
#### 5.SI和DI
si和di是8086cpu中和bx功能相近的寄存器，==si和di不能够分成两个8位寄存器来使用。==
使用si与di将字符串移动到后面的内存区中
```
assume cs:code,ds:datasg

datasg segment
	db 'welcome to masm!'
	db '...............'
datasg ends

code segment

start:
	mov ax,datasg
	mov ds,ax
	mov si,0
	mov di,16   ;设置di为16，因为需要复制的位置在16处

	mov cx,8    ;设置循环次数8  sidi为16位寄存器
    s: mov ax,[si]
    mov [di],ax
	add si,2
	add di,2
     	
	loop s

	mov ax,4c00h
	int 21h
	
;使用更少的代码
	mov ax,datasg
	mov ds,ax
	mov si,0

	mov cx,8    ;设置循环次数8  sidi为16位寄存器
    s: mov ax,[si]
    mov [si+16],ax
	add si,2
     	
	loop s

code ends

end start

```
#### 6.\[bx+si]和\[bx+di]
在前面，我们用\[bx(si或di)]和\[bx(si或di)+idata]的方式来指明一个内存单元，我们还可以用更灵活的方式：\[bx+si]和\[bx+di]
>含义：==bx中的值加上si中的值==
数学化描述：==(ax)=((ds)\*16+(bx)+(si))==
该指令也可以写成如下格式:
==mov ax,\[bx]\[si]==

#### 7.\[bx+si+idata]和\[bx+di+idata]
\[bx+si+idata]表示一个内存单元，偏移地址为(bx)+(si)+idata(即bx中的数值加上si中的数值再加上idata)
>含义：==bx+si+idata==
数学化描述：==(ax)=((ds)\*16+(bx)+(si)+idata)==
该指令也可以写成如下模式（常用）
```
mov ax,[bx+200+si]
mov ax,[200+bx+si]
mov ax,200[bx][si]
mov ax,[bx].200[si]
mov ax,[bx][si].200
```
  #### 8.不同寻址方式的灵活应用
  **将所有第一个字母大写**
 ```
  assume cs:code,ds:datasg

datasg segment
	db '1. file         '
	db '2. edit         '
	db '3. search       '
	db '4. view         '
	db '5. options      '
	db '6. help         '
datasg ends

code segment

start:
	mov ax,datasg
	mov ds,ax
	mov bx,0  ;用bx定位每一行

	mov cx,6
     s: mov al,[bx+3]
	and al,11011111B
	mov [bx+3],al
	add bx,16
	loop s

	mov ax,4c00h
	int 21h

code ends

end start

```
**将所有字母大写**
```
assume cs:code,ds:datasg

datasg segment
	db 'ibm             '
	db 'dec             '
	db 'dos             '
	db 'vax		    '
datasg ends

code segment

start:
	mov ax,datasg
	mov ds,ax
	mov bx,0  ;用bx定位每一行
	mov si,0

	mov cx,4
    s0:
	mov si,0
	mov cx,3

     s: mov al,[bx+si]
	and al,11011111B
	mov [bx+si],al
	inc si
	loop s

	add bx,16
	loop s0

	mov ax,4c00h
	int 21h

code ends

end start
```
问题在于cx的使用，我们进行了二重循环，却只用了一个循环计数器，造成再进行内层循环的时候，覆盖了外层循环的循环计数值，多用一个计数器又不可能，因为loop指令默认cx为循环计数器，怎么办呢？
>==我们应该再每次开始内层循环的时候，将外层循环的cx中的数值保存起来，再执行外层循环的loop指令前，再回复外层循环的cx数值，可以用寄存器dx来临时保存cx中的数值，改进的程序如下==
```
mov ax,datasg
	mov ds,ax
	mov bx,0  ;用bx定位每一行
	mov si,0

	mov cx,4
    s0:
	mov dx,cx     ;将外层循环cx值保存在dx中
	mov si,0
	mov cx,3

    s: 
    mov al,[bx+si]
	and al,11011111B
	mov [bx+si],al
	inc si
	loop s

	add bx,16
	mov cx,dx       ;用dx中存放的外层循环的计数器回复cx
	loop s0           ;外层循环中的loop指令将cx中的计数值减1
	
	mov ax,4c00h
	int 21h
```
如果寄存器都在被使用，那么我们该怎么办呢？
>==我们可以使用内存，将数据暂存到内存单元中，使用的时候从内存单元中恢复，这样我们就需要开辟一段空间，再次改进的程序如下==
```
assume cs:code,ds:datasg

datasg segment
	db 'ibm             '
	db 'dec             '
	db 'dos             '
	db 'vax		    '
	dw 0          ;定义一个字
datasg ends

code segment

start:
	mov ax,datasg
	mov ds,ax
	mov bx,0  ;用bx定位每一行
	mov si,0

	mov cx,4

    s0: 
    mov ds:[40H],cx     ;将外层循环的cx值保存在datasg：40H单元中
	mov si,0
	mov cx,3            ;设置为内层循环的次数

    s: 
    mov al,[bx+si]
	and al,11011111B
	mov [bx+si],al
	inc si
	loop s

	add bx,16
	mov cx,ds:[40H]      ;用datasg：40H单元中的值恢复cx
	loop s0              ;外层循环loop指令将计数值减1

	mov ax,4c00h
	int 21h


code ends

end start

```
上面的程序中，用内存单元来保存数据，可是依旧有点麻烦，如果需要保存多个数据时候，你必须要记住数据放到了哪一个单元中，这样程序容易混乱。一般来说，==我们使用栈，来进行暂存数据==我们再次改进程序
```
assume cs:code,ds:datasg

datasg segment
	db 'ibm             '
	db 'dec             '
	db 'dos             '
	db 'vax		    '
datasg ends

stacksg segment
	dw 0,0,0,0,0,0,0,0
stacksg ends

code segment

start:
	mov ax,stacksg
	mov ss,ax
	mov sp,16
	mov ax,datasg
	mov ds,ax
	mov bx,0  ;用bx定位每一行
	mov si,0

	mov cx,4

    s0: push cx          ;将cx值压栈
	mov si,0
	mov cx,3         ;设置为内层循环的次数

     s: mov al,[bx+si]
	and al,11011111B
	mov [bx+si],al
	inc si
	loop s

	add bx,16
	pop cx           ;值弹栈到cx中
	loop s0          ;外层循环loop指令将计数值减1

	mov ax,4c00h
	int 21h


code ends

end start

```