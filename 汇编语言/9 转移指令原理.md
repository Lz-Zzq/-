
可以修改IP，或者同时修改CS和IP的指令统称为转移指令。概括的讲，转移指令就是可以控制CPU执行内存中某处代码的指令
8086CPU转移行为有以下几类：
- ==只修改IP时，称为段内转移，比如 jmp ax==
- ==同时修改CS和IP时，称为段间转移，比如：jmp 1000:0==
由于转移指令对IP的修改范围不同，段内转移又分为：短转移和近转移。
- ==短转移IP的修改范围为-128~127.==
- ==近转移IP的修改范围为-32678~32767==
8086CPU的转移指令分为以下几类
- 无条件转移指令（如jmp）
- 条件转移指令
- 循环指令（如：loop）
- 过程
- 中断
这些转移指令转移的前提条件可能不同，但是转移的基本原理是相同的，我们在这一章主要通过深入学习无条件转移指令jmp来理解CPU执行转移指令的基本原理
#### 1.操作符offset
操作符offset在编程语言中是由编译器处理的符号，它的功能式取得标号的偏移地址。
==offset 标号==
```
start mov ax,offset start         ;相当于mov ax,0
s:      mov ax,offset s               ;相当于mov ax,3
```
>在上程序中，offset操作符取得了标号start 和 s 的偏移地址0与3，所以相当于右侧指令，start是代码段中的标号，它所标记的指令是代码段中的第一条指令，偏移地址为0.
s 是代码段中的标号，它所标记的指令是代码段中的第二条指令，第一条指令长度3字节，所以s偏移地址为3

填写两条指令，让改程序在运行中将s处的一条指令复制到s0处。
```
要复制的数据长度为1字，si保存了s偏移地址di保存了s0偏移地址
s :     mov ax,bx                 ;mov ax,bx占用两个字节
	 mov si,  offset  s        ;mov si 0
     mov di, offset s0       ; mov di   s0偏移地址
     (mov ax, cs:[si])         ;将cs：0处1字数据放入ax
     (mov cs:[di],ax)          ;将cs:0处1字数据放入 cs:[di]处
s0 :    nop      ;nop占用一个字节
     nop 
```
#### 2.jmp指令
jmp为无条件转移指令，可以只修改ip，也可以同时修改cs/ip。
jmp指令要给出两种信息
1. 转移的目的地址
2. 转移的距离（段间转移，段内转移，段内近转移）
#### 3.依据位移进行转移的jmp指令
==jmp short 标号（转到标号处执行指令）==
>这种格式的jmp指令实现的是段内短转移，IP修改范围-128~127，向前最多128，向后最多127个字节。short说明进行短转移。标号指明了要转移的地址，转移指令结束后，CSIP应该指向标号处的指令。
```
start : mov ax,0
     jmp short s         ;cs:ip直接指向s处
    add ax,1
s:      inc ax                   ;跳过add ax,1 直接执行inc ax
```
汇编指令jmp short s对应机器指令应该是什么样的呢？ 我们先看一下别的汇编指令和其相对应的机器指令
```
mov ax，0123h             B8 23 01
mov ax,ds:[0123h]         A1 23 01
push ds:[0123h]             FF 36 23 01
```
可以看到，在一般的汇编指令中，==idata立即数不论它是表示一个数据还是内存单元的偏移地址，都会在对应的机器指令中出现，==因为CPU执行的是机器指令，**它必须要处理这些数据或者地址。**

**机器码中不包含转移地址？**
```
start:  mov ax,0
	  jmp short s
	 add ax,1
s:       inc ax
     mov ax,4c00h
	 int 21h
```
![[汇编语言/img/Pasted image 20240812161545.png]]
>我们看到 jmp  0008表示需要转移到 cs:0008处，但是机器码好像出现了问题EB03不包含转移地址？

改写程序
```
start:   mov ax,0
	   mov bx,0
	   jmp short s
       add ax,1
s:         inc ax
```
![[汇编语言/img/Pasted image 20240812162134.png]]
>两个程序转移地址不同 0008与 000b,但是jmp指令对应的机器码都是EB03,这说明机器指令中并不包含目的转移地址，也就是说CPU不需要这个目的地址就能对IP修改。

**那么，CPU如何知道IP改为多少呢?**
分析执行过程
>1. CS:IP指向了EB03
>2. EB03进入指令缓冲器
>3. IP=IP+指令长度，CS：IP指向add ax,1
>4. CPU执行指令缓冲器中指令EB03

**CPU在执行EB03的时候是根据什么修改的IP，让其指向目标指令呢？**
>就是根据指令码中的03，需要转移的地址是CS:000B，而CPU执行EB03时候，当前IP=0008H ，如果当前IP值加3，使IP=000BH,CS:IP就可指向目标指令。在转移时候并没有告诉地址，而是告诉应该移位多少。

jmp short 标号 对应机器码中，并不包含转移地址，而是转移的位移。这个位移是根据汇编指令中的“标号”计算出来的，具体的计算方法如下 [[汇编语言附注]]
![[汇编语言/img/9bd26b761799bbd1b8f23f791fcb21a.jpg]]
实际上，“**jmp short 标号**”的功能为：（IP）=（IP）+8位位移
>1. 8位位移=标号处的地址-jmp指令后的第一个字节的地址
>2. short指明此处的位移为8位位移
>3. 8位位移的范围是-128~127，用补码表示
>4. 8位位移由编译程序在编译时算出

还有一种和“jmp short 标号”功能相近的指令格式，**jmp near ptr** 标号，它实现的是**段内近转移**
功能为：（IP）=（IP）+16位位移
>1. 16位位移=标号处的地址-jmp指令后的第一个字节的地址
>2. near ptr 指明此处的位移为16位，进行的是段内近转移
>3. 16位位移的范围为-32768~32767,用补码表示；
>4. 16位位移由编译程序在编译时算出
#### 4.转移目的的地址在指令中的jmp指令
“**jmp far ptr** 标号”实现的是**段间转移**，又称之为**远转移**。
功能如下：（CS）=标号所在段的段地址；（IP）=标号在段中的偏移地址
far ptr 指明了指令用标号的段地址和偏移地址修改CS和IP
```
start:  mov ax,0
	mov bx,0
	jmp far ptr s
	db 256 dup(0)
   s:    add ax,1
     inc ax

	mov ax,4c00h
	int 21h
```
![[汇编语言/img/Pasted image 20240812175952.png]]
jmp far ptr s 所对应的机器码，EA 0B 01 6A 07，其中包含转移的目的地址。“0B 01 6A 07”是目的地址在指令中的存储顺序，**高地址的“6A 07”是转移的段地址：076AH，低地址的“0B 01”是偏移地址：010BH**。
#### 5.转移地址在寄存器中的jmp指令
指令格式：jmp 16位 reg    #reg
功能：（IP）=（16位reg）
例如： jmp ax    好似：mov ip,ax
#### 6.转移地址在内存中的jmp指令
两种格式：
**jmp word ptr 内存单元地址（段内转移）**
功能：从内存单元地址处开始存放一个字，是转移的目的偏移地址。
内存单元地址可用寻址方式的任意一个格式给出。
比如:
```
mov ax,0123H
mov ds:[0],ax
jmp word ptr ds:[0]
```
执行后，(IP)=0123H
又比如：
```
mov ax,0123H
mov [bx],ax
jmp word ptr [bx]
```
执行后，(IP)=0123H
**jmp dword ptr 内存单元地址（段间转移）**
功能：从内存单元地址处开始存放着两个字，**高地址处的字是转移的目的段地址，低地址处是转移目的偏移地址。**
(CS)=(内存单元地址+2)
(IP)=(内存单元地址)
比如
```
mov ax,0123H
mov ds:[0],ax
mov word ptr ds:[2],0
jmp dword ptr ds:[0]
```
执行后，(CS)=0,IP=0123H,CS:IP指向 0000：0123
又比如
```
mov ax,0123H
mov [bx],ax
mov word ptr [bx+2],0
jmp dword ptr [bx]
```
执行后，(CS)=0，（IP）=0123H，CS:IP指向0000：0123

监测点：
**需要使程序中jmp执行后，CS:IP指向程序第一条指令，在data段应该定义哪些数据？**
```
assume cs:code

data segment
	?    dd 0
data ends

code segment
	start: mov ax,data
		mov ds,ax
		mov bx,0
		jmp word ptr [bx+1]
code ends
end start
```
应该定义至少占用4字节空间，并且第二个字必须是0，因为偏移地址存放在1字位置，故1字位置需要是0

**补全程序，使jmp指令执行后，CS:IP指向程序的第一条指令**
```
assume cs:code

data segment
	dd 12345678H
data ends

code segment
	start: mov ax,data
		mov ds,ax
		mov bx,0
		mov [bx], --  bx  --   -- offset start  --
		mov [bx+2], -- cs --   --  code  --
		jmp dword ptr ds:[0]
code ends
end start
```
bx中存放偏移地址，应该是0，故bx；bx+2处是高位，存放段地址，故cs
也可以偏移地址使用start，start偏移地址在0处，程序入口。段地址可以用code，code段的地址就是程序第一条指令的段地址
#### 7.jcxz指令
jcxz指令为有条件转移指令，所有的有条件转移指令都是短转移，在对应的机器码中包含转移的位移，而不是目的地址，对IP的修改范围都是：-128~127
指令格式：**jcxz 标号   如果(cx)=0，转移到标号处执行**
操作：**当（cx）=0时，（IP）=（IP）+8位位移**；
>8位位移=标号处的地址-jcxz指令后的第一个字节的地址
 8位位移的范围为-128~127，用补码表示
8位位移由编译程序在编译时算出

**当 cx！=0时，什么也不做**（程序向下执行）
我们看出jcxz 标号 功能相当于：
`if（（cx）==0））jmp short 标号`

利用jcxz指令，在2000H段中查找第一个值为0的字节，找到后，将他存储在dx中
```
start:  mov ax,2000H
	mov ds,ax
	mov bx,0
	s:
	mov cl,[bx]  ;将bx处字节放入cl
	mov ch,[0]   ;将ch置为0的20
	jcxz ok
	inc bx

	jmp short s
	ok: mov dx,bx

	mov ax,4c00h
	int 21h
```
#### 8.loop指令
loop指令为循环指令，**所有的循环指令都是段内短转移**，在对应机器码中包含转移的位移，而不是目的地址。对IP修改范围都是-128~127
指令格式：**loop 标号 (cx)=(cx)-1，如果(cx)!=0，转移到标号处执行**
操作：
>(cx)=(cx)-1
>如果cx!=0,(IP)=(IP)+8位位移

8位位移=标号处的地址-loop指令后的第一个字节的地址；
8位位移的范围-128~127，用补码表示
8位位移由编译程序在编译时算出
如果(cx)=0，什么也不做(程序向下执行)
loop 标号相当于
```
cx --
if（(cx)!=0） jmp short 标号;
```
#### 9.根据位移进行转移的意义
前面我们讲到
jmp short 标号
jmp near ptr 标号
jcxz 标号
loop 标号
**他们对IP的修改是根据转移目的地址和转移起始地址之间的位移来进行的。**，在机器码中不包含目的地址，包含的是到目的地址的位移。
这种设计，方便了程序段在内存中的浮动装配
例如：
```
	mov cx,6         B9 06 00
	mov ax,10h     B8 10 00
s :     add ax,ax        01 C0
	loop s             E2 FC
```
这段程序装在内存中的不同位置都可以正确执行，因为loop s在执行时只涉及s的位移(-4,前移4个字节，补码表示FCH)，而不是s的地址。如果loop s 的机器码中包含的是s的地址，则对程序段在内存中的偏移地址有严格的限制。如果s处指令不在目的地处，程序就会出错。如果包含的是转移地址则不会有这个问题，因为s处的指令实际地址是多少，loop指令的转移位移是不变的。
#### 10.编译器对转移位移超界的检测
如果源程序中转移范围超界，编译时候，编译器将报错
错误例如:
```
start : jmp short s
	db 128 dup (0)
s:      mov ax,offffh
```
jmp short s的转移范围是-128~127，IP最多向后移动127字节

#### 实验8 分析一个奇怪的数据
分析下面程序，运行前思考：可以正确返回吗？
为什么是这种结果？
```
assume cs:codesg
codesg segment

	mov ax,4c00h
	int 21h

start: mov ax,0
s:	nop
	nop

	mov di,offset s
	mov si,offset s2
	mov ax,cs:[si]
	mov cs:[di],ax

s0:     jmp short s

s1:     mov ax,0
	int 21h
	mov ax,0

s2:     jmp short s1  ;向上移位
	nop

codesg ends
end start
```
执行jmp short s1时候，是在第一个nop处执行的，随后机器码移位指令向上移位，所以可以正常退出

#### 实验9 根据材料编程
![[汇编语言/img/02448233dfeadc9ad9d83b0ba6c69e0.jpg]]![[汇编语言/img/aeb326ddee72c33629e279bb83bc639.jpg]]
```
assume cs:codesg,ds:data

codesg segment
	
data segment
	db 119,02,65H,02H,6CH,02H,99,02H,6fH,02H,6DH,02H,65H,02H,20H,02,116
	db 02H,111,02H,20H,02,6dH,02H,61H,02H,73H,02H,6dH,02H,21H,02

	db 119,36,65H,36,6CH,36,99,36,6fH,36,6DH,36,65H,36,20H,36,116
	db 36,111,36,20H,36,6dH,36,61H,36,73H,36,6dH,36,21H,36
	
	db 119,113,65H,113,6CH,113,99,113,6fH,113,6DH,113,65H,113,20H,113,116
	db 113,111,113,20H,113,6dH,113,61H,113,73H,113,6dH,113,21H,113
data ends

start: 

	mov ax,data
	mov ds,ax
	mov ax,47104
	mov es,ax
	mov bx,1760
	
	mov di,0

	mov cx,16
	s:
	mov ax,[di]
	mov es:[bx+di+64],ax
	mov ax,[di+32]
	mov es:[bx+di+224],ax
	mov ax,[di+64]
	mov es:[bx+di+384],ax

	add di,2
	
	loop s

	
	mov ax,4c00h
	int 21h

codesg ends
end start
```
**B8000H~BFFFFH共32KB空间，为80x25采择字符模式的显示缓冲区，像这个空间写入数据，写入的内容会立即出现再显示器上**